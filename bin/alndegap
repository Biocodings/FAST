#!/usr/bin/perl -w
use POSIX;
use Getopt::Long qw(:config bundling require_order auto_version);
use Pod::Usage;
use FAST;
use FAST::Bio::AlignIO;
use FAST::Bio::SeqIO;
use FAST::Bio::UnivAln;
use Carp;
use strict;

use vars qw($VERSION $DESC $NAME $COMMAND $DATE);
$VERSION = $FAST::VERSION; 
$DESC    = "";
$NAME    = $0;
$NAME    =~ s/^.*\///;
$COMMAND = join " ",$NAME,@ARGV;
$DATE = POSIX::strftime("%c",localtime());

use constant { true => 1, false => 0 };

## DEFAULT OPTION VALUES
my $def_format  = $FAST::DEF_FORMAT;  #7/1/13 "fasta";
my $def_logname = $FAST::DEF_LOGNAME; #7/1/13 "FAST.log.txt";

## OPTION VARIABLES
my $man                  = undef;  # --man
my $help                 = undef;  # -h
my $moltype              = undef;  # -m, in case bioperl can't tell
my $log                  = undef;        # -l
my $logname              = $def_logname; # -L
my $comment              = undef;        # -C
my $gap                  = undef;
my $degap                = undef;
my $remove_gapped        = undef;
my $indices              = undef;


GetOptions('help|h'         		 => \$help, 
	   'man'            		 => \$man,
	   'moltype|m=s'                 => sub{  my (undef,$val) = @_; 
						  die "$NAME: --moltype or -m option must be either \"dna\", \"rna\" or \"protein\"" 
						    unless $val =~ /dna|rna|protein/i; 
						  $moltype = $val;
						},
	   'log|l'                       => \$log,
	   'logname|L=s'                 => \$logname,
	   'comment|C=s'                 => \$comment,
     'gap-freq|g=f'                => \$gap,
     'remove-gapped|a'             => \$remove_gapped,
     'degap|d'                     => \$degap,
     'indices|i'                   => \$indices,

	  ) 
  or pod2usage(2);
		  
pod2usage(1) if $help;
pod2usage(-verbose => 2) if $man;
my $fromSTDIN = ((-t STDIN) ? false : true);
pod2usage("$NAME: Requires at least one argument FILE [FILE2â€¦FILEN] unless input from STDIN.\n") if (!($fromSTDIN) && (@ARGV == 0));
pod2usage("$NAME: Requires exactly zero arguments if input is from STDIN.\n") if ($fromSTDIN && (@ARGV != 0));

&FAST::log($logname, $DATE, $COMMAND, $comment) if ($log); 

if ($gap) {
  if ($gap < 0 or $gap > 1) {
    croak "ERROR: $NAME -g <number> expects <number> to be a positive fraction gap frequency threshold between 0 and 1.\n"; 
  }
  elsif ($gap == 0) {
    undef $gap;
    $remove_gapped = 1;
  }
  elsif ($gap == 1) {
    undef $gap;
  }
}  

#my $OUT = FAST::Bio::AlignIO->newFh('-format' => $def_format);
my $IN;
unless (@ARGV) {   
	$IN = FAST::Bio::AlignIO->new(-fh => *STDIN{IO}, '-format' => $def_format);
}

while ($IN or @ARGV) {
  if (@ARGV) {
    my $file = shift (@ARGV);
    unless (-e $file) {
      warn "$NAME: Could not find file $file. Skipping.\n";
      next;
    }
    elsif ($moltype) {
      $IN = FAST::Bio::AlignIO->new(-file => $file, '-format' => $def_format, '-alphabet' => $moltype);
    }
    else {
      $IN = FAST::Bio::AlignIO->new(-file => $file, '-format' => $def_format);
    }
  }
  if ($IN) { 
    while (my $saln = $IN->next_aln) {
      my $index_selection_fxn;
      my $site_selection_fxn;
      my $last;
      my $subaln;
      my @seqs = $saln->each_seq();
      my $aln  = FAST::Bio::UnivAln->new('-seqs' => \@seqs);
      if ($indices) { ## DON'T REMOVE SITES; PRINT INDICES
    if ($gap) {
      croak "ERROR: $NAME -g <number> doesn't work with -i. Sorry.\n";
    }
    elsif ($remove_gapped) { ## CONSIDER SITES CONTAINING AT LEAST ONE GAP
      $index_selection_fxn = "gap_free_inds";
    }
    else {
      $index_selection_fxn = "no_allgap_inds";
    }
    my @sites = @{$aln->$index_selection_fxn()};
    my @coords = ();
    do {
      my $first = shift @sites;
      my $second = $first;
      do {
  $last = $second;
  $second = shift @sites;
      } until (!$second or $second > ($last + 1));
      unshift @sites, $second if ($second);
      if ($first == $last) {
  push @coords, "$first";
      }
      else {
  push @coords, "$first-$last";
      }
    } 
      until (!@sites);
    print "@coords \n";
    exit;
  }
  elsif ($degap) {
    my $out = FAST::Bio::SeqIO->newFh(-format => $def_format);
    foreach my $seq (@seqs) {
      my $seqseq = $seq->seq;
      $seqseq =~ s/[^A-Za-z]+//g;
      $seq->seq($seqseq);
      print $out $seq;
    }
    exit;
  }
  elsif ($gap) {
    sub _setup_gap_frequency_filter {
      my $threshold = shift;
      return sub {
  my @chars = @{ $_[0] };
  my $gf = grep {$_ eq "-"} @chars;
  return ($gf/@chars < $threshold);
      }
    }
    my $gap_freq_filter = _setup_gap_frequency_filter($gap);
    $subaln = new FAST::Bio::UnivAln(-seqs=>scalar($aln->seqs([],$gap_freq_filter)), -row_ids=>$aln->row_ids);
  }
  else {
    if ($remove_gapped) { ## CONSIDER SITES CONTAINING AT LEAST ONE GAP
      $site_selection_fxn = "gap_free_sites";
    }
    else { ## DEFAULT, REMOVE SITES CONTAINING ALL GAPS
      $site_selection_fxn = "no_allgap_sites";    
    }
    $subaln = new FAST::Bio::UnivAln(-seqs=>scalar($aln->$site_selection_fxn()), -row_ids=>$aln->row_ids);
  }
  my $layout = $subaln->layout();
  ## FIX FASTA PRINT BUG (IDs SHOULD GO RIGHT AFTER '>') 
  $layout =~ s/^>\s+/>/gm;
  print "$layout";
    }
    undef $IN;
  }
}



__END__

=head1 NAME

B<alndegap> - removes gap containing sites from a seqeunce alignment

=head1 SYNOPSIS

B<alndegap> [options] [MULTIFASTA-FILE...]

=head1 DESCRIPTION

B<alndegap> takes multifasta format sequence or alignment data as input, and
will remove gap characters from sequence. Gaps can be all removed or removed
based on a frequency of gaps per column.

Options specific to B<alndegap>:
  B<-g>, B<--gap-freq>                  removes gap columns based on an indicated frequency                
  B<-a>, B<--remove-gapped>             removes columns with gap sites
  B<-d>, B<--degap>                     removes all gaps from individual sequences
  B<-i>, B<--indices>                   used with -a will show sites left after removal of gaps

Options general to FAST:
  B<-h>, B<--help>                  	 print a brief help message
  B<--man>             	           	 print full documentation
  B<--version>                           print version
  B<-l>, B<--log>                        create/append to logfile	
  B<-L>, B<--logname>=<string>           use logfile name <string>
  B<-C>, B<--comment>=<string>           save comment <string> to log
  B<--format>=<format>                 	 use alternative format for input  
  B<--moltype>=<[dna|rna|protein]>       specify input sequence type

=head1 INPUT AND OUTPUT

B<alndegap> is part of FAST, the FAST Analysis of Sequences Toolbox, based
on Bioperl. Most core FAST utilities expect input and return output in
multifasta format. Input can occur in one or more files or on
STDIN. Output occurs to STDOUT. The FAST utility B<fasconvert> can
reformat other formats to and from multifasta.

=head1 OPTIONS

=over 8

=item B<-g>,
      B<--gap-freq>

This option will remove gap sites from an alignment based on an indicated
frequency from 0 to 1.

=item B<-a>,
      B<--remove-gapped>

This option will remove sites (columns) within an alignment that contain gap
characters. Preserving the alignment.

=item B<-d>
      B<--degap>

This option will remove all gap characters from an alignment (alignment will
not be preserved).

=item B<-i>
      B<--indices>

This option is to be used with remove-gapped (-a), and will list the regions 
of sequence that will remain after gap columns are removed.

=item B<-h>,
      B<--help>

Print a brief help message and exit.

=item B<--man>

Print the manual page and exit.

=item B<--version>

Print version information and exit.

=item B<-l>,
      B<--log>

Creates, or appends to, a generic FAST logfile in the current working
directory. The logfile records date/time of execution, full command
with options and arguments, and an optional comment.

=item B<-L [string]>,
      B<--logname=[string]>

Use [string] as the name of the logfile. Default is "FAST.log.txt".

=item B<-C [string]>,
      B<--comment=[string]>

Include comment [string] in logfile. No comment is saved by default.

=item B<--format=[format]> 		  

Use alternative format for input. See man page for "fasconvert" for
allowed formats. This is for convenience; the FAST tools are designed
to exchange data in Fasta format, and "fasta" is the default format
for this tool.

=item B<-m [dna|rna|protein]>,
      B<--moltype=[dna|rna|protein]> 		  

Specify the type of sequence on input (should not be needed in most
cases, but sometimes Bioperl cannot guess and complains when
processing data).

=back

=head1 EXAMPLES

Remove columns that contain 50% or more gaps from the alignment contained in data.fas

=over 8

B<alndegap> -g 0.50 data.fas  

=back

=head1 SEE ALSO

=over 8

=item C<man perlre>

=item C<perldoc perlre>

Documentation on perl regular expressions.

=item C<man FAST>

=item C<perldoc FAST>

Introduction and cookbook for FAST

=item L<The FAST Home Page|http://compbio.ucmerced.edu/ardell/FAST>"

=back 

=head1 CITING

If you use FAST, please cite I<Lawrence et al. (2014). FAST: FAST Analysis of
Sequences Toolbox.> and Bioperl I<Stajich et al.>. 

=cut
