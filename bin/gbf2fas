#!/usr/bin/perl -w
use POSIX;
use Getopt::Long qw(:config bundling require_order auto_version);
use Pod::Usage;
use FAST;
use FAST::Bio::SeqIO;
use Carp;
use strict;

use vars qw($VERSION $DESC $NAME $COMMAND $DATE);
$VERSION = $FAST::VERSION; 
$DESC    = "Extract subsequences for potentially split features from GenBank file(s) with tag-based filtering.\n";
$NAME    = $0;
$NAME    =~ s/^.*\///;
$COMMAND = join " ",$NAME,@ARGV;
$DATE = POSIX::strftime("%c",localtime());

use constant { true => 1, false => 0 };

## DEFAULT OPTION VALUES
my $def_format  = $FAST::DEF_FORMAT;  #7/1/13 "fasta";
my $def_logname = $FAST::DEF_LOGNAME; #7/1/13 "FAST.log.txt";

## OPTION VARIABLES
my $man                  = undef;  # --man
my $help                 = undef;  # -h
my $moltype              = undef;  # -m, in case bioperl can't tell
my $format               = $def_format;  # --format
my $log                  = undef;        # -l
my $logname              = $def_logname; # -L
my $comment              = undef;        # -C
my $tag_match            = undef; #-t
my $case                 = undef;
my $location             = undef;
my $primary              = undef;
my $context              = undef;

GetOptions('help|h'         		 => \$help, 
	   'man'            		 => \$man,
	   'moltype|m=s'                 => sub{  my (undef,$val) = @_; 
						  die "$NAME: --moltype or -m option must be either \"dna\", \"rna\" or \"protein\"" 
						    unless $val =~ /dna|rna|protein/i; 
						  $moltype = $val;
						},
	   'format=s'                    => \$format,
	   'log'                         => \$log,
	   'logname|L=s'                 => \$logname,
	   'comment|C=s'                 => \$comment,
     'tag|t=s'                     => \$tag_match,
     'case-insensitive|i'          => \$case,
     'location|l'                  => \$location,
     'primary|p=s'                 => \$primary,

	  ) 
  or pod2usage(2);
		  
pod2usage(1) if $help;
pod2usage(-verbose => 2) if $man;

my $fromSTDIN = ((-t STDIN) ? false : true);

pod2usage("$NAME: Requires at least one argument when taking input from terminal. Try \"perldoc $NAME\"") if (!($fromSTDIN) && (@ARGV < 1));
pod2usage("$NAME: Requires at least one argument. Try \"perldoc $NAME\"") if ($fromSTDIN && @ARGV != 0);

&FAST::log($logname, $DATE, $COMMAND, $comment) if ($log); 

my @tag = ();
my @op = ();
my @tag_regex = ();


if ($tag_match) { 
  foreach my $tvp (split /\+/,$tag_match) {
    unless ($tvp =~ /[=\^]/) {
      carp "Bad format in -t option";
      die;
    }
    my ($tag,$op,$tag_regex) = split /([=\^])/,$tvp;
    unless ($tag and $op and $tag_regex) {
      carp "Bad format in -t option";
      die;
    }
    push @tag,$tag; 
    push @op,($op eq "=" ? 1 : 0);
    push @tag_regex,($case ? qr/$tag_regex/i : qr/$tag_regex/);
  }
}

my $OUT = FAST::Bio::SeqIO->newFh('-format' => 'fasta');
my $IN;
unless (@ARGV) {
    $IN = FAST::Bio::SeqIO->new(-fh => *STDIN{IO}, '-format' => 'genbank');
}

my $featseq = undef;
my $id = undef;
while ($IN or @ARGV) {
  if (@ARGV) {
    my $file = shift (@ARGV);
    unless (-e $file) {
      warn "$NAME: Could not find file $file. Skipping.\n";
      next;
    }
      $IN = FAST::Bio::SeqIO->new(-file => $file, '-format' => 'genbank');
  }
  if ($IN) { 
    while (my $seqobj = $IN->next_seq()) {
      FEAT: foreach my $feat ( $primary ? 
         (grep { $_->primary_tag =~ /$primary/i } $seqobj->get_SeqFeatures() ) :
         ($seqobj->get_SeqFeatures())
       )  {
    foreach my $i (0..$#tag) {
      next FEAT unless (
       (
        ($feat->has_tag($tag[$i]) and $op[$i] and grep {/$tag_regex[$i]/} $feat->get_tag_values($tag[$i]))
        or
        ($feat->has_tag($tag[$i]) and not $op[$i] and not grep {/$tag_regex[$i]/} $feat->get_tag_values($tag[$i]))
       )
      );
    }
    my $loc_string  = $feat->location->to_FTstring;
    $loc_string =~ s/omplement//;
    ## HANDLE CONTEXT
    ## 12-1-14 $context is never defined so this code doesn't do anything
    ##if ($context != 0  and $feat->start > $feat->end) {
    ##  $feat->start($feat->start + $context);
    ##  $feat->end  ($feat->end   - $context);
    ##}
    ##elsif ($context != 0) {
    ##  $feat->start($feat->start - $context);
    ##  $feat->end  ($feat->end   + $context);
    ##}

    $featseq = $feat->spliced_seq;
    ## SET DISPLAY ID TO RETURN ALL TAG INFORMATION; THIS CAN BE OPTIONALLY RESTRICTED
    $id = join "_", grep {/geneid/i} $feat->get_tagset_values($feat->get_all_tags());
    $id ||= join "_", grep {/gi/i} $feat->get_tagset_values($feat->get_all_tags());
    $id ||= join "_", $feat->get_tagset_values("gene");
    $id ||= join "_", $feat->get_tagset_values("product");
    $id ||= $loc_string;
    $featseq->display_id($id);
    $featseq->desc(join " ",(map {join "=",$_,(join "","\"",(join ";",grep {!/geneid/i} $feat->get_tagset_values($_)),"\"")} $feat->get_all_tags()),($location ? "location=\"$loc_string\"" : '')); 
    
    print $OUT $featseq;
  }
    }
    undef $IN;
  }
}



__END__

=head1 NAME

B<gbf2fas> - extract subsequences for potentially split features from GenBank file(s) with tag-based filtering

=head1 SYNOPSIS

B<gbf2fas> [OPTIONS]... [-t <tag1>[=^]<perl-regex1>+<tag2>[=^]<perl-regex2>+...] < genbank-file"

=head1 DESCRIPTION

B<gbf2fas> extract subsequences for potentially split features from GenBank file(s) with tag-based filtering

Options specific to name:
  B<-t>, B<--tag=<string>>                filter only features that have all specified tags
  B<-i>, B<--case-insensitive>            case-insensitive pattern matching for tag-value regexes with -t option
  B<-l>, B<--location>                    print genomic location strings in the sequence descriptions
  B<-p>, B<--primary=<string>>           filter only features with primary tags matching given perl-regex

Options general to FAST:
  B<-h>, B<--help>                  	 print a brief help message
  B<--man>             	           print full documentation
  B<--version>                         print version
  B<-l>, B<--log>                         create/append to logfile	
  B<-L>, B<--logname=<string>>            use logfile name <string>
  B<-C>, B<--comment=<string>>            save comment <string> to log
  B<--format=<format>>                 use alternative format for input  
  B<--moltype=<[dna|rna|protein]>>      specify input sequence type

=head1 INPUT AND OUTPUT

B<gbf2fas> is part of FAST, the FAST Analysis of Sequences Toolbox, based
on Bioperl. Most core FAST utilities expect input and return output in
multifasta format. Input can occur in one or more files or on
STDIN. Output occurs to STDOUT. The FAST utility B<fasconvert> can
reformat other formats to and from multifasta.

=head1 OPTIONS

=over 8

=item B<-t [tag1][=^][perl-regex1]+[tag2][=^][perl-regex2]>,
      B<--tag [tag1][=^][perl-regex1]+[tag2][=^][perl-regex2]>

Filter only features that have all specified tags: tag1, tag2, etc
and, for each tag, at least one value that either matches (=) or no value that matches (^)
their specific perl-regexes: perl-regex1, perl-regex2, etc.
Examples of tags: 'product', 'note', 'gene', 'db_xref'

=item B<-i>,
      B<--case-insensitive>

Enable case-insensitive matching of tags.

=item B<-l>,
      B<--location>

Append genomic location strings in the outputted sequence descriptions.

=item B<-p>,
      B<--primary>

Filter only features with primary tags matching given perl-regex. 
Ex: 'CDS', 'RNA', 'CDS|RNA'
This regex is always matched case-insensitively.

=item B<-h>,
      B<--help>

Print a brief help message and exit.

=item B<--man>

Print the manual page and exit.

=item B<--version>

Print version information and exit.

=item B<-l>,
      B<--log>

Creates, or appends to, a generic FAST logfile in the current working
directory. The logfile records date/time of execution, full command
with options and arguments, and an optional comment.

=item B<-L [string]>,
      B<--logname=[string]>

Use [string] as the name of the logfile. Default is "FAST.log.txt".

=item B<-C [string]>,
      B<--comment=[string]>

Include comment [string] in logfile. No comment is saved by default.

=item B<--format=[format]> 		  

Use alternative format for input. See man page for "fasconvert" for
allowed formats. This is for convenience; the FAST tools are designed
to exchange data in Fasta format, and "fasta" is the default format
for this tool.

=item B<-m [dna|rna|protein]>,
      B<--moltype=[dna|rna|protein]> 		  

Specify the type of sequence on input (should not be needed in most
cases, but sometimes Bioperl cannot guess and complains when
processing data).

=back

=head1 EXAMPLES

Output the 5' and 3' UTR sequences:

=over 8

B<gbf2fas> -p UTR > data.dna.gb

=back

Output all sequences under the feature "exon" and add location labels:

=over 8

B<gbf2fas> -l p exon > data.dna.gb

=back

Output all feature sequences with an "organism" tag set to "Stylonychia lemnae":

=over 8

B<gbf2fas> -t "organism=Stylonychia lemnae" > data.dna.gb


=back


=head1 SEE ALSO

=over 8

=item C<man perlre>

=item C<perldoc perlre>

Documentation on perl regular expressions.

=item C<man FAST>

=item C<perldoc FAST>

Introduction and cookbook for FAST

=item L<The FAST Home Page|http://compbio.ucmerced.edu/ardell/FAST>"

=back 

=head1 CITING

If you use FAST, please cite I<Lawrence et al. (2014). FAST: FAST Analysis of
Sequences Toolbox.> and Bioperl I<Stajich et al.>. 

=cut
