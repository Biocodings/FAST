#!/usr/bin/perl -w
use POSIX;
use Getopt::Long qw(:config bundling require_order auto_version);
use Pod::Usage;
use FAST;
use FAST::Bio::SeqIO;
use strict;

use vars qw($VERSION $DESC $NAME $COMMAND $DATE);
$VERSION = $FAST::VERSION; 
$DESC    = "Extract neighboring features from Genbank files corresponding to reg-ex match on tags.\n";
$NAME    = $0;
$NAME    =~ s/^.*\///;
$COMMAND = join " ",$NAME,@ARGV;
$DATE = POSIX::strftime("%c",localtime());

use constant { true => 1, false => 0 };

## DEFAULT OPTION VALUES
my $def_format  = $FAST::DEF_FORMAT;  #7/1/13 "fasta";
my $def_logname = $FAST::DEF_LOGNAME; #7/1/13 "FAST.log.txt";

## OPTION VARIABLES
my $man                  = undef;  # --man
my $help                 = undef;  # -h
my $log                  = undef;        # -l
my $logname              = $def_logname; # -L
my $comment              = undef;        # -C
my $radius               = 1; #-r
my $case                 = undef;
my $tag_match            = undef;
my $begin_match          = undef;
my $end_match            = undef;


GetOptions('help|h'         		 => \$help, 
	   'man'            		 => \$man,
	   'log|l'                       => \$log,
	   'logname|L=s'                 => \$logname,
	   'comment|C=s'                 => \$comment,
	   'radius|r=i'                  => \$radius,
	   'case-insensitive|i'          => \$case,
	   'begin|b=i'                   => \$begin_match,
	   'end|e=i'                     => \$end_match,
	   'tag|t=s'                     => \$tag_match,                

	  ) 
  or pod2usage(2);
		  
pod2usage(1) if $help;
pod2usage(-verbose => 2) if $man;
pod2usage("Usage: $0 -[hi] [-t <tag>] [-r radius [1]] \'perl-reg-exp\' < genbank-file. Try $NAME -h for help.") if (!(-t STDIN) && (@ARGV != 1));

&FAST::log($logname, $DATE, $COMMAND, $comment) if ($log); 

my $start;
my $stop;
my $gene;
my $product;
my $note;

my $tag = 'note';
if ($tag_match){
  $tag = $tag_match;
}

my $begin = my $end = undef;
$begin_match and $begin = $begin_match;
$end_match and $end = $end_match;

my $IN = FAST::Bio::SeqIO->new(-fh => *STDIN{IO}, '-format' => 'genbank'); ##READ FROM STDIN
my $re = ($case ? qr/$ARGV[0]/i : qr/$ARGV[0]/);

while(my $seqobj = $IN->next_seq()) {
  my @feat = grep {$_->primary_tag eq 'CDS' or $_->primary_tag =~ /RNA/} $seqobj->top_SeqFeatures();
  for my $i (0 .. $#feat) {
    my $feat = $feat[$i];
    next if ($begin and $begin > $feat->start);
    next if ($end   and $end <   $feat->end);
    if ($feat->has_tag($tag)) {
      foreach my $value ( $feat->each_tag_value($tag) ) {
        my $k;
  if ($value =~ $re) {
    ++$k;
    print "\n\nMatch $k:\n";
    for my $j (($i - $radius) .. ($i + $radius)) {
      if ($feat[$j]) { #Check that feature exists
      my $lfeat = $feat[$j];
      $start = $lfeat->start;
      $stop  = $lfeat->end;
      $gene     = $lfeat->has_tag('gene')    ? join ' + ',$lfeat->each_tag_value('gene')    : ' '; 
      $product  = $lfeat->has_tag('product') ? join ' + ',$lfeat->each_tag_value('product') : ' '; 
      $note     = $lfeat->has_tag('note')    ? join ' + ',$lfeat->each_tag_value('note')    : ' '; 
      write;
    }
    }
  }
      }
    }
  }
}

format STDOUT = 
@>>>>>>>>  @>>>>>>>>  @>>>>>>  @>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>  @>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
$start,$stop,$gene,$product,$note
.




__END__

=head1 NAME

B<gbfgrep> - extract neighboring features from Genbank file(s) corresponding to reg-ex match on tags-values

=head1 SYNOPSIS

B<gbfgrep> -[hi] [-t <tag>] [-b int] [-e int] [-r radius [1]] perl-reg-exp < genbank-file

=head1 DESCRIPTION

B<gbfgrep> - extract neighboring features from Genbank file(s) corresponding to reg-ex match on tag-values.
Radius of neighboring features to be extracted is set by the -r, --radius option with a default value of 1. 
Only tags found under the primary tag CDS or primary tags containing the word RNA are searchable.  

Options specific to name:
  B<-r>, B<--radius>=<integer>          print <r> features before matched tag and <r> features after matched tag. Default r=1 
  B<-i>, B<--case-insensitive>          case-insensitive pattern matching for tag-value regexes with -t option
  B<-b>, B<--begin>=<integer>           only return matches that start after this sequence location 
  B<-e>, B<--end>=<integer>             only return matches that end before this sequence location
  B<-t>, B<--tag>=<string>              tag used for tag-value reg-ex match. 

Options general to FAST:
  B<-h>, B<--help>                  	 print a brief help message
  B<--man>             	           print full documentation
  B<--version>                         print version
  B<-l>, B<--log>                         create/append to logfile	
  B<-L>, B<--logname>=<string>            use logfile name <string>
  B<-C>, B<--comment>=<string>            save comment <string> to log
  B<--format>=<format>                 use alternative format for input  
  B<--moltype>=<[dna|rna|protein]>      specify input sequence type

=head1 INPUT AND OUTPUT

B<gbfgrep> is part of FAST, the FAST Analysis of Sequences Toolbox, based
on Bioperl. Most core FAST utilities expect input and return output in
multifasta format. Input can occur in one or more files or on
STDIN. Output occurs to STDOUT. The FAST utility B<fasconvert> can
reformat other formats to and from multifasta.

=head1 OPTIONS

=over 8

=item B<-r [integer]>,
      B<--radius [integer]>

 print <r> features before matched tag and <r> features after matched tag


=item B<-i>,
      B<--case-insensitive>

Enable case-insensitive matching of tag-values.

=item B<-b [integer]>,
      B<--begin [integer]>

Only return matches that start after this sequence location.

=item B<-e [integer]>,
      B<--end [integer]>

Only return matches that end before this sequence location.

=item B<-t [string]>,
      B<--tag [string]>

Tag used for value reg-ex match.

=item B<-h>,
      B<--help>

Print a brief help message and exit.

=item B<--man>

Print the manual page and exit.

=item B<--version>

Print version information and exit.

=item B<-l>,
      B<--log>

Creates, or appends to, a generic FAST logfile in the current working
directory. The logfile records date/time of execution, full command
with options and arguments, and an optional comment.

=item B<-L [string]>,
      B<--logname=[string]>

Use [string] as the name of the logfile. Default is "FAST.log.txt".

=item B<-C [string]>,
      B<--comment=[string]>

Include comment [string] in logfile. No comment is saved by default.

=item B<--format=[format]> 		  

Use alternative format for input. See man page for "fasconvert" for
allowed formats. This is for convenience; the FAST tools are designed
to exchange data in Fasta format, and "fasta" is the default format
for this tool.

=item B<-m [dna|rna|protein]>,
      B<--moltype=[dna|rna|protein]> 		  

Specify the type of sequence on input (should not be needed in most
cases, but sometimes Bioperl cannot guess and complains when
processing data).

=back

=head1 EXAMPLES

Extract up to three features before and after tags that contain the word polmerase from the genbank file AF194338.1.gb:  

=over 8

gbfgrep -r 3 -t"product" 'polymerase' < t/data/AF194338.1.gb

=back


=head1 SEE ALSO

=over 8

=item C<man perlre>

=item C<perldoc perlre>

Documentation on perl regular expressions.

=item C<man FAST>

=item C<perldoc FAST>

Introduction and cookbook for FAST

=item L<The FAST Home Page|http://compbio.ucmerced.edu/ardell/FAST>"

=back 

=head1 CITING

If you use FAST, please cite I<Lawrence et al. (2014). FAST: FAST Analysis of
Sequences Toolbox.> and Bioperl I<Stajich et al.>. 

=cut
