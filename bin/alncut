#!/usr/bin/perl -w
use POSIX;
use Getopt::Long qw(:config bundling require_order auto_version);
use Pod::Usage;
use FAST;
use FAST::Bio::AlignIO;
use FAST::Bio::UnivAln;
use strict;

use vars qw($VERSION $DESC $NAME $COMMAND $DATE);
$VERSION = $FAST::VERSION; 
$DESC    = "";
$NAME    = $0;
$NAME    =~ s/^.*\///;
$COMMAND = join " ",$NAME,@ARGV;
$DATE = POSIX::strftime("%c",localtime());

use constant { true => 1, false => 0 };

## DEFAULT OPTION VALUES
my $def_format  = $FAST::DEF_FORMAT;  #7/1/13 "fasta";
my $def_logname = $FAST::DEF_LOGNAME; #7/1/13 "FAST.log.txt";

## OPTION VARIABLES
my $man                  = undef;  # --man
my $help                 = undef;  # -h
my $moltype              = undef;  # -m, in case bioperl can't tell
my $format               = $def_format;  # --format
my $log                  = undef;        # -l
my $logname              = $def_logname; # -L
my $comment              = undef;        # -C
my $index                = undef;
my $threshold            = 1; #default value


GetOptions('help|h'         		 => \$help, 
	   'man'            		 => \$man,
	   'moltype|m=s'                 => sub{  my (undef,$val) = @_; 
						  die "$NAME: --moltype or -m option must be either \"dna\", \"rna\" or \"protein\"" 
						    unless $val =~ /dna|rna|protein/i; 
						  $moltype = $val;
						},
	   'format=s'                    => \$format,
	   'log|l'                       => \$log,
	   'logname|L=s'                 => \$logname,
	   'comment|C=s'                 => \$comment,
	   'indices|i'                     => \$index,
	   'threshold|t=f'               => \$threshold, #if the fraction of the letters in the column that form a
	                                                 #(relative) majority is NOT >= $threshold the column is retained.
	                                                 #The default $threshold is 1.

	  ) 
  or pod2usage(2);
		  
pod2usage(1) if $help;
pod2usage(-verbose => 2) if $man;
pod2usage("$NAME: Expects at least one input filename or glob. Try \"perldoc $NAME\"") if ((-t STDIN) && (@ARGV < 1));
pod2usage("$NAME: Expects no arguments when reading from STDIN. Try \"perldoc $NAME\"") if (!(-t STDIN) && @ARGV != 0);

&FAST::log($logname, $DATE, $COMMAND, $comment) if ($log); 


my $IN;
unless (@ARGV) {   
  if ($moltype){
    $IN = FAST::Bio::AlignIO->new(-fh => *STDIN{IO}, '-format' => $def_format, '-alphabet' => $moltype);
  }
  else{
    $IN = FAST::Bio::AlignIO->new(-fh => *STDIN{IO}, '-format' => $def_format);
  }
}

while ($IN or @ARGV) {
  if (@ARGV) {
    my $file = shift (@ARGV);
    unless (-e $file) {
      warn "$NAME: Could not find file $file. Skipping.\n";
      next;
    }
    elsif ($moltype) {
      $IN = FAST::Bio::AlignIO->new(-file => $file, '-format' => $def_format, '-alphabet' => $moltype);
    }
    else {
      $IN = FAST::Bio::AlignIO->new(-file => $file, '-format' => $def_format);
    }
  }
  if ($IN) { 
    while (my $saln = $IN->next_aln){
     my @seqs = $saln->each_seq();
     my $aln  = FAST::Bio::UnivAln->new('-seqs' => \@seqs);

     if ($index) {
       my $inds = $aln->var_inds($threshold);
       print STDERR join ' ',@{$inds},"\n";
      }
     my $subaln = new FAST::Bio::UnivAln(-seqs=>scalar($aln->var_sites($threshold)), -row_ids=>$aln->row_ids);
     #$subaln->ffmt($opt_f); 
     my $layout = $subaln->layout();
     ## FIX FASTA PRINT BUG (IDs SHOULD GO RIGHT AFTER '>') 
     $layout =~ s/^>\s+/>/gm;
     print "$layout";
    }
    undef $IN;
  }
}



__END__

=head1 NAME

alnsite - Extracts only variable sites from an alignment

=head1 SYNOPSIS

B<alnsite> [options] [MULTIFASTA-FILE...]

=head1 DESCRIPTION

B<alnsite> takes multifasta format sequence or alignment data as input,

Options specific to B<alnsite>:

  B<-i>, B<--indices>                 print alignment positions to stderr that remain after removal of invariable columns based on threshold value        
  B<-t>, B<--threshold>=<float>       if the fraction of the letters in the column that form a (relative) majority is NOT >= <threshold> the column is retained. The default is 1.
>>>>>>> f341ff1e93c305c49550d58d451e5450d6103dd0:bin/alnsite

Options general to FAST:
  B<-h>, B<--help>                  	 print a brief help message
  B<--man>             	           print full documentation
  B<--version>                         print version
  B<-l>, B<--log>                         create/append to logfile	
  B<-L>, B<--logname>=<string>            use logfile name <string>
  B<-C>, B<--comment>=<string>            save comment <string> to log
  B<--format>=<format>                 use alternative format for input  
  B<--moltype>=<[dna|rna|protein]>      specify input sequence type

=head1 INPUT AND OUTPUT

B<alnsite> is part of FAST, the FAST Analysis of Sequences Toolbox, based
on Bioperl. Most core FAST utilities expect input and return output in
multifasta format. Input can occur in one or more files or on
STDIN. Output occurs to STDOUT. The FAST utility B<fasconvert> can
reformat other formats to and from multifasta.

=head1 OPTIONS

=over 8

=item B<-i>
      B<--indices>

Print alignment positions to stderr that remain after removal of invariable
 columns based on threshold value.

=item B<-t>
      B<--threshold>

If the fraction of the letters in the column that form a (relative) majority 
is NOT >= <threshold> the column is retained. The default is 1.

=item B<-h>,
      B<--help>

Print a brief help message and exit.

=item B<--man>

Print the manual page and exit.

=item B<--version>

Print version information and exit.

=item B<-l>,
      B<--log>

Creates, or appends to, a generic FAST logfile in the current working
directory. The logfile records date/time of execution, full command
with options and arguments, and an optional comment.

=item B<-L [string]>,
      B<--logname=[string]>

Use [string] as the name of the logfile. Default is "FAST.log.txt".

=item B<-C [string]>,
      B<--comment=[string]>

Include comment [string] in logfile. No comment is saved by default.

=item B<--format=[format]> 		  

Use alternative format for input. See man page for "fasconvert" for
allowed formats. This is for convenience; the FAST tools are designed
to exchange data in Fasta format, and "fasta" is the default format
for this tool.

=item B<-m [dna|rna|protein]>,
      B<--moltype=[dna|rna|protein]> 		  

Specify the type of sequence on input (should not be needed in most
cases, but sometimes Bioperl cannot guess and complains when
processing data).

=back

=head1 EXAMPLES

Remove alignment columns that contain 75% or more of a single character 
from alignment file data.fas and save column indices remaining after 
removal to file indices.txt: 

=over 8

B<alnsite> -it 0.75 data.fas 2> indices.txt

=back



=head1 SEE ALSO

=over 8

=item C<man perlre>

=item C<perldoc perlre>

Documentation on perl regular expressions.

=item C<man FAST>

=item C<perldoc FAST>

Introduction and cookbook for FAST

=item L<The FAST Home Page|http://compbio.ucmerced.edu/ardell/FAST>"

=back 

=head1 CITING

If you use FAST, please cite I<Ardell (2013). FAST: FAST Analysis of
Sequences Toolbox. Bioinformatics> and Bioperl I<Stajich et al.>.

=cut
